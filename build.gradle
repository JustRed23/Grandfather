import java.time.ZonedDateTime
import java.time.format.DateTimeFormatter

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "gradle.plugin.net.nemerosa:versioning:2.14.0"
    }
}

plugins {
    id 'java'
    id 'application'
    id 'net.nemerosa.versioning' version '2.14.0'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

mainClassName = 'dev.JustRed23.grandfather.App'
group 'dev.JustRed23'

allprojects {
    updateVersion()
}

def updateVersion() {
    ant.propertyfile(file: "./src/main/resources/application.properties") {
        entry( key: "version", value: versioning.info.build)
        entry( key: "buildTime", value: DateTimeFormatter.ISO_OFFSET_DATE_TIME.format(ZonedDateTime.now()))
    }
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven { url 'https://m2.duncte123.dev/releases' }
}

dependencies {
    //Engine
    implementation 'com.github.JustRed23:Stonebrick:v1.8'

    //JDA
    implementation 'net.dv8tion:JDA:5.0.0-beta.9'
    implementation 'ch.qos.logback:logback-classic:1.4.5'
    implementation 'me.duncte123:botCommons:2.3.14'

    //Music
    implementation 'com.github.walkyst:lavaplayer-fork:1.4.1'
    implementation 'com.github.natanbc:lavadsp:0.7.4'

    //Class handling
    implementation 'org.reflections:reflections:0.10.2'

    //Youtube Data API
    implementation 'com.google.apis:google-api-services-youtube:v3-rev20230123-2.0.0'
}

shadowJar {
    archiveFileName = 'Grandfather.jar'
    manifest {
        attributes('Implementation-Title': project.name, 'Implementation-Version': versioning.info.build)
    }
}