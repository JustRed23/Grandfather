import java.time.ZonedDateTime
import java.time.format.DateTimeFormatter

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "gradle.plugin.net.nemerosa:versioning:2.14.0"
    }
}

plugins {
    id 'java'
    id 'net.nemerosa.versioning' version '2.14.0'
}

group 'dev.JustRed23'

allprojects {
    updateVersion()
}

def updateVersion() {
    ant.propertyfile(file: "./src/main/resources/application.properties") {
        entry( key: "version", value: versioning.info.build)
        entry( key: "buildTime", value: DateTimeFormatter.ISO_OFFSET_DATE_TIME.format(ZonedDateTime.now()))
    }
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven { url 'https://m2.duncte123.dev/releases' }
}

dependencies {
    //Engine
    implementation 'com.github.JustRed23:Stonebrick:v1.3'

    //JDA
    implementation 'net.dv8tion:JDA:5.0.0-alpha.17'
    implementation 'ch.qos.logback:logback-classic:1.2.11'
    implementation 'me.duncte123:botCommons:2.3.14'

    //Music
    implementation 'com.github.walkyst:lavaplayer-fork:1.3.98.3'
    implementation 'com.github.natanbc:lavadsp:0.7.4'

    //Class handling
    implementation 'org.reflections:reflections:0.10.2'

    //Youtube Data API
    implementation 'com.google.apis:google-api-services-youtube:v3-rev20220719-1.32.1'
}

jar {
    manifest {
        attributes('Main-Class': 'dev.JustRed23.grandfather.App')
        attributes('Implementation-Title': project.name, 'Implementation-Version': versioning.info.build)
    }
}

task fatJar(type: Jar) {
    manifest.from jar.manifest
    classifier = 'all'
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    } {
        exclude "META-INF/**"
        exclude "module-info.class"
    }
    with jar
}